#version 450 core
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_EXT_shader_16bit_storage : require

layout (local_size_x_id = 253, local_size_y_id = 254,
        local_size_z_id = 255) in;

layout (constant_id = 0) const int act = 0;
layout (constant_id = 1) const int transpose_b = 0;
layout (constant_id = 2) const float scale = 1.0;
layout (constant_id = 3) const float offset = 0;

layout (binding = 0) readonly buffer InputTensor0
{
  float16_t input_tensor0[];
};
layout (binding = 1) readonly buffer InputTensor1
{
  float16_t input_tensor1[];
};
layout (binding = 2) writeonly buffer OutputTensor0
{
  float16_t output_tensor0[];
};

layout (push_constant) uniform constants
{
  int C;
  int M;
  int N;
  int K;
};

void
main ()
{
  uint gid_x = gl_WorkGroupID.x;
  uint gid_y = gl_WorkGroupID.y;
  uint gid_z = gl_WorkGroupID.z;

  if (gid_z >= C || transpose_b != 1)
    {
      return;
    }

  float sum = .0;
  for (uint i = gl_SubgroupInvocationID.x; i < K; i += gl_SubgroupSize)
    {
      if (i < K)
        {
          sum += (float (input_tensor0[gid_z * M * K + gid_y * K + i])
                  * float (input_tensor1[gid_z * N * K + gid_x * K + i]));
        }
    }

  float v = subgroupAdd (sum) * scale + offset;
  v = act == 1 ? v / (1.0 + exp (-v)) : v;

  if (subgroupElect ())
    {
      output_tensor0[gid_z * M * N + gid_y * N + gid_x] = float16_t (v);
    }
}
