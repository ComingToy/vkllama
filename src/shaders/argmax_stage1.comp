#version 450 core
layout (local_size_x_id = 253, local_size_y_id = 254,
        local_size_z_id = 255) in;

// max = 0, min = 1
layout (constant_id = 0) const int op_type = 0;
layout (push_constant) uniform constants
{
  uint C;
  uint H;
  uint W;
};

struct Pair
{
  uint i;
  float v;
};


layout (binding = 0) readonly buffer InputTensor0 { Pair input0[]; };
layout (binding = 1) writeonly buffer OutputTensor0 { uint output0[]; };

// block size = [1, 128, 1]
void
main (void)
{
  uint tid_x = gl_GlobalInvocationID.x;
  uint tid_y = gl_GlobalInvocationID.y;
  uint tid_z = gl_GlobalInvocationID.z;

  if (tid_x >= W || tid_y >= H || tid_z >= C)
    {
      return;
    }

  uint input_base = tid_z * H * W + tid_y * W;
  uint output_base = tid_z * H + tid_y;

  float v = input0[input_base].v;
  uint r = 0;
  for (uint i = 1; i < W; ++i)
    {
      if ((op_type == 0 && input0[input_base + i].v > v)
          || (op_type == 1 && input0[input_base + i].v < v))
        {
          v = input0[input_base + i].v;
          r = i;
        }
    }

  output0[output_base] = input0[input_base + r].i;
}
